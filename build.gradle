plugins {
    id 'org.ajoberstar.reckon' version '0.16.1'
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

allprojects {
    pluginManager.withPlugin('java') {

        configurations {
            // See https://github.com/gradle/gradle/issues/10861
            // To avoid publishing the internal-platform
            internalPlatform {
                visible = false
                canBeConsumed = false
                canBeResolved = false
            }
            compileClasspath.extendsFrom(internalPlatform)
            runtimeClasspath.extendsFrom(internalPlatform)
            testCompileClasspath.extendsFrom(internalPlatform)
            testRuntimeClasspath.extendsFrom(internalPlatform)
        }

        java {
            sourceCompatibility = JavaVersion.VERSION_11
            withSourcesJar()
            withJavadocJar()
        }

        repositories {
            mavenCentral()
            maven {
                url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                content {
                    includeGroup 'com.contentgrid.opa-java-client'
                }
            }
        }
    }

    pluginManager.withPlugin('maven-publish') {
        apply from: "${rootDir}/gradle/publish.gradle"
    }
}


